; EAB WHDLoad Magazines OCS Menu Package Install Script
; -----------------------------------------------------
;
; Author: Henrik Noerfjand Stengaard
; Date: 2021-12-05
;
; This script extracts and installs zip archives for EAB WHDLoad Magazines OCS Menu Package.


; Reset eab menu
SET eabmenuags2="Yes"
SET eabmenuhst="Yes"
SET eabmenuigame="Yes"
SET eabmenuwhdload="Yes"
SET eabmenuigameset="320x128"


; Build eab menu
LAB eabmenu

echo "" NOLINE >"T:_eabmenu"
echo "Install AGS2 Menu:               $eabmenuags2" >>"T:_eabmenu"
echo "Install HST Launcher Menu:       $eabmenuhst" >>"T:_eabmenu"
echo "Install iGame Screenshots:       $eabmenuigame" >>"T:_eabmenu"
echo "Configure WHDLoad.prefs:         $eabmenuwhdload" >>"T:_eabmenu"
echo "--------------------------------------------" >>"T:_eabmenu"
echo "Selected iGame Screenshot Set:   $eabmenuigameset" >>"T:_eabmenu"
echo "--------------------------------------------" >>"T:_eabmenu"
echo "Start Installation" >>"T:_eabmenu"


; Show eab menu
set eabmenu "`RequestList TITLE="EAB WHDLoad Magazines OCS Menu Package" LISTFILE="T:_eabmenu" WIDTH=480 LINES=16`"
delete "T:_eabmenu" >NIL:

; Install AGS2 Menu option
IF "$eabmenu" EQ 1 VAL
  IF "$eabmenuags2" EQ "Yes"
    SET eabmenuags2="No"
  ELSE
    SET eabmenuags2="Yes"
  ENDIF
ENDIF

; Install HST Menu option
IF "$eabmenu" EQ 2 VAL
  IF "$eabmenuhst" EQ "Yes"
    SET eabmenuhst="No"
  ELSE
    SET eabmenuhst="Yes"
  ENDIF
ENDIF

; Install iGame Screenshots option
IF "$eabmenu" EQ 3 VAL
  IF "$eabmenuigame" EQ "Yes"
    SET eabmenuigame="No"
  ELSE
    SET eabmenuigame="Yes"
  ENDIF
ENDIF

; Configure WHDLoad.prefs option
IF "$eabmenu" EQ 4 VAL
  IF "$eabmenuwhdload" EQ "Yes"
    SET eabmenuwhdload="No"
  ELSE
    SET eabmenuwhdload="Yes"
  ENDIF
ENDIF

; Selected iGame Screenshot Set option
IF "$eabmenu" EQ 6 VAL
  IF "$eabmenuigameset" EQ "320x128"
    SET eabmenuigameset="320x256"
  ELSE
    SET eabmenuigameset="320x128"
  ENDIF
ENDIF

; Start Installation option
IF "$eabmenu" EQ 8 VAL
  SKIP install
ENDIF

SKIP BACK eabmenu


; Install
; -------

LAB install

; Copy readme and screenshots to locale help for EAB WHDLoad Magazines OCS Menu
echo "Installing Readme..."
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu
  MakePath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale/Help.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale/Help FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale/Help
  ENDIF
ENDIF
IF NOT EXISTS SYSTEMDIR:Locale.info
  IF EXISTS SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info
    makeicon >NIL: SYSTEMDIR:Locale FROM SYSTEMDIR:Prefs/Env-Archive/Sys/def_drawer.info 
  ELSE
    makeicon >NIL: SYSTEMDIR:Locale
  ENDIF
ENDIF
copy >NIL: PACKAGEDIR:README SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu
copy >NIL: PACKAGEDIR:README.guide SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu
copy >NIL: PACKAGEDIR:README.info SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu
IF NOT EXISTS SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu/screenshots
  MakePath >NIL: SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu/screenshots
ENDIF
copy >NIL: PACKAGEDIR:screenshots/#?.iff SYSTEMDIR:Locale/Help/EAB-WHDLoad-Magazines-OCS-Menu/screenshots


; Install system files
; --------------------

echo "Installing System Files..."
unzip -qq -o -x PACKAGEDIR:system-files.zip -d SYSTEMDIR:


; Configure WHDLoad arguments
; ---------------------------

; Create systemdir whdload prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad
  MakePath >NIL: SYSTEMDIR:Prefs/WHDLoad
ENDIF

; Create default whdloadargs, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/WHDLoad/whdloadargs
  echo "Preload" >SYSTEMDIR:Prefs/WHDLoad/whdloadargs
ENDIF


; Install Frontends Magazines
; ---------------------------

echo "Installing Frontends Magazines..."
unzip -qq -o -x PACKAGEDIR:frontends-magazines.zip -d FRONTENDSDIR:
echo "Installing Frontends Magazines All..."
unzip -qq -o -x PACKAGEDIR:frontends-magazines-all.zip -d FRONTENDSDIR:

; paths
set hstwbpath "`execute INSTALLDIR:S/CombinePath "$FRONTENDSDIR" "HstWB"`"

; backdrop leave out hstwb
Execute INSTALLDIR:S/Backdrop-LeaveOut "$hstwbpath"


; Configure icons
; ---------------

; Install magazines icons
unzip -qq -o -x PACKAGEDIR:magazines-icons.zip -d MAGAZINESDIR:

; Add icon for programs drawer, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Programs.info
  rename >NIL: SYSTEMDIR:Programs.info.new SYSTEMDIR:Programs.info
ELSE
  delete >NIL: SYSTEMDIR:Programs.info.new
ENDIF

; Add icon for configuration drawer, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Programs/Configuration.info
  rename >NIL: SYSTEMDIR:Programs/Configuration.info.new SYSTEMDIR:Programs/Configuration.info
ELSE
  delete >NIL: SYSTEMDIR:Programs/Configuration.info.new
ENDIF


; Configure WHDLoad.prefs
; -----------------------

; Skip to install AGS2, if configure whdload.prefs is set to no
IF NOT "$eabmenuwhdload" EQ "Yes"
  SKIP installags2
ENDIF

echo "Configuring WHDLoad.prefs..."
IF NOT EXISTS SYSTEMDIR:S
  MakePath >NIL: SYSTEMDIR:S
ENDIF
IF NOT EXISTS "SYSTEMDIR:S/WHDLoad.prefs"
  Copy PACKAGEDIR:WHDLoad.prefs SYSTEMDIR:S
ENDIF

; Disable existing splash deplay
rep "SYSTEMDIR:S/WHDLoad.prefs" "SplashDelay" ";SplashDelay"

; Set splash deplay in WHDLoad.prefs
echo "SplashDelay=50" >>SYSTEMDIR:S/WHDLoad.prefs


; Install AGS2
; ------------

LAB installags2

; Skip to install hst, if install ags2 is set to no
IF NOT "$eabmenuags2" EQ "Yes"
  SKIP installhstlauncher
ENDIF

echo "Installing AGS2 Magazines Menu..."
set ags2magsmenudir "`execute INSTALLDIR:S/CombinePath "$MENUSDIR" "AGS2Magazines"`"
IF NOT EXISTS "$ags2magsmenudir"
  MakePath "$ags2magsmenudir" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:ags2.zip -d "$ags2magsmenudir"
IF NOT EXISTS "$ags2magsmenudir/Music"
  MakePath "$ags2magsmenudir/Music" >NIL:
ENDIF
IF NOT EXISTS "$ags2magsmenudir/.Favourites.ags"
  IF NOT EXISTS "$ags2magsmenudir/.Favourites.ag_"
    MakePath >NIL: "$ags2magsmenudir/.Favourites.ag_"
  ENDIF
ENDIF

echo "Installing AGS2 Magazines All Menu..."
unzip -qq -o -x PACKAGEDIR:ags2-magazines-all.zip -d "$ags2magsmenudir"

echo "Configuring AGS2 Magazines Menu..."

; Create systemdir ags2 prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2
  MakePath >NIL: SYSTEMDIR:Prefs/AGS2
ENDIF

; Create ags2music prefs to turn on music in ags2 menu
IF NOT EXISTS SYSTEMDIR:Prefs/AGS2/ags2music
  echo "" NOLINE > SYSTEMDIR:Prefs/AGS2/ags2music
ENDIF

; Add or update AGS2Menus.cfg
search SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2Magazines=" >NIL:
IF WARN
  echo "AGS2Magazines=$ags2magsmenudir" >>SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg
ELSE
  rep SYSTEMDIR:Prefs/AGS2/AGS2Menus.cfg "AGS2Magazines=" "AGS2Magazines=$ags2magsmenudir*N; "
ENDIF


; Install HST Launcher
; --------------------

LAB installhstlauncher

; Skip to install igame, if install hst is set to no
IF NOT "$eabmenuhst" EQ "Yes"
  SKIP installigame
ENDIF

echo "Installing HST Launcher Magazines Menu..."
set hstmagsmenudir "`execute INSTALLDIR:S/CombinePath "$MENUSDIR" "HSTMagazines"`"
IF NOT EXISTS "$hstmagsmenudir"
  MakePath "$hstmagsmenudir" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:hst.zip -d "$hstmagsmenudir"
IF NOT EXISTS "$hstmagsmenudir/Music"
  MakePath "$hstmagsmenudir/Music" >NIL:
ENDIF

set hstmags1mbmenudir "`execute INSTALLDIR:S/CombinePath "$MENUSDIR" "HSTMagazines1MB"`"
IF NOT EXISTS "$hstmags1mbmenudir"
  MakePath "$hstmags1mbmenudir" >NIL:
ENDIF
unzip -qq -o -x PACKAGEDIR:hst.zip -d "$hstmags1mbmenudir"
IF NOT EXISTS "$hstmags1mbmenudir/Music"
  MakePath "$hstmags1mbmenudir/Music" >NIL:
ENDIF

echo "Installing HST Magazines All Menu..."
unzip -qq -o -x PACKAGEDIR:hst-magazines-all.zip -d "$hstmagsmenudir"
echo "Installing HST Magazines 1MB Menu..."
unzip -qq -o -x PACKAGEDIR:hst-magazines-1mb.zip -d "$hstmags1mbmenudir"

echo "Configuring HST Launcher Magazines Menu..."

; Create systemdir hst launcher prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/HSTLauncher
  MakePath >NIL: SYSTEMDIR:Prefs/HSTLauncher
ENDIF

; Add or update HSTLauncherMenus.cfg
search SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTMagazines=" >NIL:
IF WARN
  echo "HSTMagazines=$hstmagsmenudir" >>SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTMagazines=" "HSTMagazines=$hstmagsmenudir*N; "
ENDIF
search SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTMagazines1MB=" >NIL:
IF WARN
  echo "HSTMagazines1MB=$hstmags1mbmenudir" >>SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/HSTLauncher/HSTLauncherMenus.cfg "HSTMagazines1MB=" "HSTMagazines1MB=$hstmags1mbmenudir*N; "
ENDIF

 
; Install iGame
; -------------

LAB installigame

; Skip to configure assigns, if install igame is set to no
IF NOT "$eabmenuigame" EQ "Yes"
  SKIP configureassigns
ENDIF

; Install iGame Screenshot Menu
IF "$eabmenuigameset" EQ "320x128"
  echo "Installing iGame Magazines All 320x128 Screenshots..."
  unzip -qq -o -x PACKAGEDIR:igame-320x128-all.zip -d MAGAZINESDIR:
ELSE
  echo "Installing iGame Magazines All 320x256 Screenshots..."
  unzip -qq -o -x PACKAGEDIR:igame-320x256-all.zip -d MAGAZINESDIR:
ENDIF

echo "Configuring iGame Magazines Menu..."

; Create systemdir igame prefs directory, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:Prefs/iGame
  MakePath >NIL: SYSTEMDIR:Prefs/iGame
ENDIF

; Add or update iGameMenus.cfg
search SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameMagazines=" >NIL:
IF WARN
  echo "iGameMagazines=$FRONTENDSDIR" >>SYSTEMDIR:Prefs/iGame/iGameMenus.cfg
ELSE
  rep SYSTEMDIR:Prefs/iGame/iGameMenus.cfg "iGameMagazines=" "iGameMagazines=$FRONTENDSDIR*N; "
ENDIF
 

; Configure assigns
; -----------------

LAB configureassigns

echo "Configuring Assigns..."

; Create assign startup, if it doesn't exist
IF NOT EXISTS SYSTEMDIR:S/Assign-Startup
	echo "" NOLINE >SYSTEMDIR:S/Assign-Startup
ENDIF

; Add execute assign startup to startup sequence, if it not ClassicWB and doesn't contain execute assign startup
search SYSTEMDIR:S/Startup-Sequence "ClassicWB" >NIL:
IF WARN
  search SYSTEMDIR:S/Startup-Sequence "execute S:Assign-Startup" >NIL:
  IF WARN
    rep SYSTEMDIR:S/Startup-Sequence "BindDrivers" "IF EXISTS S:Assign-Startup*N  execute S:Assign-Startup*NENDIF*N*NBindDrivers"
  ENDIF
ENDIF

; Add or update A-Magazines to assign
search SYSTEMDIR:S/Assign-Startup "A-Magazines:" >NIL:
IF WARN  
	echo "Assign >NIL: A-Magazines: $MAGAZINESDIR" >>SYSTEMDIR:S/Assign-Startup
ELSE
  setenv updateassign `RequestChoice "Update assign" "A-Magazines: assign already exists and must be updated to*Npath '$MAGAZINESDIR'.*N*NNote: Updating assign requires a reboot after*Ninstallation is done!*N*NDo you want to update A-Magazines: assign?" "Yes|No"`
  IF "$updateassign" EQ "1"
    echo "$MAGAZINESDIR" >T:_magazinesdir
    set assignmagsdir "`sed "s/\//\\\//g" T:_magazinesdir`"
    sed "s/\([Aa]-[Mm][Aa][Gg][Aa][Zz][Ii][Nn][Ee][Ss]:\)/\1 $assignmagsdir;/" "SYSTEMDIR:S/Assign-Startup" >"T:Assign-Startup"
    copy >NIL: "T:Assign-Startup" "SYSTEMDIR:S/Assign-Startup"
  ENDIF   
ENDIF
